
################################################################################
# INCLUDE Packages
################################################################################


defineTest( glew ) {
  !exists ( c:\glew ) {
    error (ERROR: GLEW not found or wrong path entry in OpenMesh\qmake\packages\packages.Windows!)
  }
  INCLUDEPATH *= c:\glew\include
  export(INCLUDEPATH)
  LIBS *= -Lc:\glew\lib -lglew32
  export(LIBS)
}

defineTest( qt ) {
  CONFIG *= uitools
  export(CONFIG)
  QT += opengl network script sql
  export(QT)
}

defineTest( glut ) {
  !exists ( c:\glut ) {
    error (ERROR: GLUT not found or wrong path entry in OpenMesh\qmake\packages\packages.Windows!)
  }
  INCLUDEPATH *= c:\glut\include
  export(INCLUDEPATH)
  LIBS *= -Lc:\glut\lib -lglut32
  export(LIBS)
}

defineTest( openmp ) {
	QMAKE_CXXFLAGS_DEBUG += /openmp
	QMAKE_CXXFLAGS_RELEASE += /openmp
	export(QMAKE_CXXFLAGS_DEBUG)
	export(QMAKE_CXXFLAGS_RELEASE)
}

defineTest( openmesh ) {
  DEFINES += _USE_MATH_DEFINES NOMINMAX
  QMAKE_LIBDIR += $${TOPDIR}/OpenMesh/Core/lib/$${BUILDDIRECTORY}
  LIBS+= -L$${TOPDIR}/OpenMesh/Core/lib/$${BUILDDIRECTORY} -lCore 
  LIBS+= -L$${TOPDIR}/OpenMesh/Tools/lib/$${BUILDDIRECTORY} -lTools 
  export(DEFINES)
  export(QMAKE_LIBDIR)
  export(LIBS)
}

